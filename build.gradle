import groovy.json.JsonSlurper
import net.fabricmc.loom.task.RemapJarTask
import net.ornithemc.ploceus.Constants
import net.ornithemc.ploceus.GameSide
import net.ornithemc.ploceus.OslVersionCache;

plugins {
    id 'fabric-loom' version "1.4.+"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.freefair.lombok' version "8.1.0"
    id 'maven-publish'
    id 'ploceus' version "1.4.7"
    id 'com.modrinth.minotaur' version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

base.archivesName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
    maven {
        name = "legacy-fabric"
        url = "https://maven.legacyfabric.net"
    }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

loom {
    mods {
        "legacy-lwjgl3" {
            sourceSet("main")
        }
    }
}

project.ext.lwjglVersion = "3.3.2"

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.ornithemc:feather:${project.mappings}"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation("org.javassist:javassist:3.29.2-GA")
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    for (String platform : ["linux", "windows", "macos", "windows-arm64", "macos-arm64"]){
        runtimeOnly "org.lwjgl:lwjgl::natives-$platform"
        runtimeOnly "org.lwjgl:lwjgl-glfw::natives-$platform"
        runtimeOnly "org.lwjgl:lwjgl-openal::natives-$platform"
        runtimeOnly "org.lwjgl:lwjgl-opengl::natives-$platform"
    }

    ploceus.addCommonLibraries()
    //modRuntimeOnly("net.ornithemc:osl:$osl_version")
    dependOsl(osl_version)


}

configurations.configureEach {
    exclude group: "org.lwjgl.lwjgl"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) it.options.release = 8
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

shadowJar {
    minimize {
        exclude(dependency("org.lwjgl:lwjgl:$lwjglVersion"))
        exclude(dependency("org.lwjgl:lwjgl-glfw:$lwjglVersion"))
        exclude(dependency("org.lwjgl:lwjgl-openal:$lwjglVersion"))
        exclude(dependency("org.lwjgl:lwjgl-opengl:$lwjglVersion"))
    }
    dependencies {
        include(dependency("org.javassist:javassist:3.29.2-GA"))

        include(dependency("org.lwjgl:lwjgl:$lwjglVersion"))
        include(dependency("org.lwjgl:lwjgl-glfw:$lwjglVersion"))
        include(dependency("org.lwjgl:lwjgl-openal:$lwjglVersion"))
        include(dependency("org.lwjgl:lwjgl-opengl:$lwjglVersion"))
    }
}

tasks.register('remapShadowJar', RemapJarTask) {
    dependsOn shadowJar
    input = shadowJar.archiveFile
    setArchiveClassifier("all-remapped")
    addNestedDependencies = false
}

tasks.build.dependsOn remapShadowJar

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            from components.java
            artifact remapShadowJar
        }
    }

    // select the repositories you want to publish to
    repositories {
        maven {
            name = "owlMaven"
            def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
            url = "https://moehreag.duckdns.org/maven/" + repository
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.modrinth.dependsOn publish

static def fetchGameVersions(){

    List<String> versions = new ArrayList<>()
    InputStream stream = URI.create("https://meta.ornithemc.net/v3/versions/game").toURL().openStream()
    JsonSlurper slurper = new JsonSlurper()
    List<Version> json = slurper.parse(stream) as List<Version>
    json.forEach(ve -> {
        if (ve.stable) {
            def v = ve.version
            String[] parts = v.split("\\.");
            int minor = Integer.parseInt(parts[1]);
            if (minor >= 8 && minor < 13) {
                versions.add(v)
            }
        }
    })
    return versions
}

class Version {
    String version
    boolean stable
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "lpiIRiAZ"
    versionType = "release"
    uploadFile = remapShadowJar
    additionalFiles = [sourcesJar]
    gameVersions = fetchGameVersions()
    loaders = ["fabric", "quilt"]
    dependencies {
        required.project "osl"
    }
}

// This exists to have OSL in a runtimeOnly configuration
def dependOsl(String version) throws Exception {
    def oslVersions = project.ext.properties['oslVersions']
    if (oslVersions == null){
        oslVersions = project.ext.oslVersions = new OslVersionCache(project, ploceus)
    }
    for (Map.Entry<String, String> entry : oslVersions.getDependencies(version).entrySet()) {
        String module = entry.getKey();
        String baseVersion = entry.getValue();
        String moduleVersion = oslVersions.getVersion(module, baseVersion, GameSide.MERGED);

        // not all modules cover all Minecraft versions
        // so check if a valid module version exists for
        // this Minecraft version before adding the dependency
        if (moduleVersion != null) {
            project.getDependencies().add("modRuntimeOnly", String.format("%s:%s:%s",
                    Constants.OSL_MAVEN_GROUP,
                    module,
                    moduleVersion));
        }
    }
}