import groovy.json.JsonSlurper
import net.fabricmc.loom.task.RemapJarTask
import net.ornithemc.ploceus.GameSide

plugins {
    id 'fabric-loom' version "1.5-SNAPSHOT"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'io.freefair.lombok' version "8.1.0"
    id 'maven-publish'
    id 'ploceus' version "1.5-SNAPSHOT"
    id 'com.modrinth.minotaur' version "2.+"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

base.archivesName = project.archives_base_name
version = project.mod_version+"+"+project.minecraft_version
group = project.maven_group

repositories {
    maven {
        name = "legacy-fabric"
        url = "https://maven.legacyfabric.net"
    }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

loom {
    mods {
        "legacy-lwjgl3" {
            sourceSet("main")
        }
    }

    clientOnlyMinecraftJar()
}

ploceus {
    clientOnlyMappings()
}

project.ext.lwjglVersion = "3.3.2"

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings project.loom.layered {
        mappings "net.ornithemc:feather:${project.rootProject.minecraft_version}-client+build.${project.feather_build}:v2"
        addLayer project.ploceus.nestedMappings() // remove this line if not using nests
    }
    nests "net.ornithemc:nests:${project.rootProject.minecraft_version}-client+build.${project.nests_build}" // remove this line if not using nests

    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    implementation("org.javassist:javassist:3.29.2-GA")
    implementation platform("org.lwjgl:lwjgl-bom:$lwjglVersion")

    implementation "org.lwjgl:lwjgl"
    implementation "org.lwjgl:lwjgl-glfw"
    implementation "org.lwjgl:lwjgl-openal"
    implementation "org.lwjgl:lwjgl-opengl"
    for (String platform : ["linux", "windows", "macos", "windows-arm64", "macos-arm64"]){
        runtimeOnly "org.lwjgl:lwjgl::natives-$platform"
        runtimeOnly "org.lwjgl:lwjgl-glfw::natives-$platform"
        runtimeOnly "org.lwjgl:lwjgl-openal::natives-$platform"
        runtimeOnly "org.lwjgl:lwjgl-opengl::natives-$platform"
    }

    ploceus.addCommonLibraries()
    ploceus.dependOsl("modRuntimeOnly", osl_version, GameSide.CLIENT)


}

configurations.configureEach {
    exclude group: "org.lwjgl.lwjgl"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) it.options.release = 8
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

shadowJar {
    minimize {
        exclude(dependency("org.lwjgl:lwjgl:$lwjglVersion"))
        exclude(dependency("org.lwjgl:lwjgl-glfw:$lwjglVersion"))
        exclude(dependency("org.lwjgl:lwjgl-openal:$lwjglVersion"))
        exclude(dependency("org.lwjgl:lwjgl-opengl:$lwjglVersion"))
    }
    dependencies {
        include(dependency("org.javassist:javassist:3.29.2-GA"))

        include(dependency("org.lwjgl:lwjgl:$lwjglVersion"))
        include(dependency("org.lwjgl:lwjgl-glfw:$lwjglVersion"))
        include(dependency("org.lwjgl:lwjgl-openal:$lwjglVersion"))
        include(dependency("org.lwjgl:lwjgl-opengl:$lwjglVersion"))
    }
}

tasks.register('remapShadowJar', RemapJarTask) {
    dependsOn shadowJar
    input = shadowJar.archiveFile
    setArchiveClassifier("all-remapped")
    addNestedDependencies = false
}

tasks.build.dependsOn remapShadowJar

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) { publication ->
            from components.java
            artifact remapShadowJar
        }
    }

    // select the repositories you want to publish to
    repositories {
        maven {
            name = "owlMaven"
            def repository = project.version.toString().contains("beta") || project.version.toString().contains("alpha") ? "snapshots" : "releases"
            url = "https://moehreag.duckdns.org/maven/" + repository
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
}

tasks.modrinth.dependsOn publish

static def fetchGameVersions(){

    /*List<String> versions = new ArrayList<>()
    InputStream stream = URI.create("https://piston-meta.mojang.com/mc/game/version_manifest_v2.json").toURL().openStream()
    JsonSlurper slurper = new JsonSlurper()
    Map<String, ?> map = slurper.parse(stream) as Map<String, ?>
    List<Version> json = map.get("versions") as List<Version>
    json.forEach(ve -> {
        if (ve.type == "release") {
            def v = ve.id
            String[] parts = v.split("\\.");
            int minor = Integer.parseInt(parts[1]); // TODO fix this mess.
            if (minor >= 2 && minor < 3) {
                versions.add(v)
            }
        }
    })*/
    return ["b1.7.3"]
}

class Version {
    String id
    String type
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN")
    projectId = "lpiIRiAZ"
    versionType = "release"
    uploadFile = remapShadowJar
    additionalFiles = [sourcesJar]
    gameVersions = fetchGameVersions()
    loaders = ["fabric", "quilt"]
    dependencies {
        optional.project "osl"
    }
}